.Object Name: BUILD_
*
*===================================================================================================
:INCLUDE,ecab$,B,1 .                                                                         INCLUDE
@.
@.    #---------------------------------------------------------------------------------------------
@.    #  BUILD
@.    #  Written: 04/07/2014
@.    #---------------------------------------------------------------------------------------------
@.
@.    # Test Setup
@     ldv <ctl>s80='BUILD' .
@     ldv <file>s256='d:\users\winne\sublwatcher\site-e\sublwatcher\Sublime\2b56.bis' .
@     call 0001 (<ctl>,<file>,'',<status>h1) .
@     GTO END .
@.
@0001:(<ctl>,<file>,<runname>,<status>) .
@     rar,0,E,2 0199 .                                                          sets-run-abort
@     rer,0,E,2 0001 .                                                          sets-run-error
@.
@     lcv 't@' <ctl> ' ' <pos>i3  inc <pos> .
@     ldv <rev>h5=<ctl>(<pos>-0) .
@.
@     rsr,SUBLCOMMON_ configVars_ .
@     call,SUBLCOMMON_ appPath_ (<user>,<apppath>s256) .
@     ldv <pth>s256=<datadir>'\'<app>'\' .
@.
@.    Get bis script and make sure has correct extension
@     call,SUBLCOMMON_ getFileName_ (<file>,<run>s60) .                 Extracts File Name from path
@     ldv <tfile>s256=<file>  lcv '' <tfile> <run>/'' .
@     lcv,0180 'b99' <run> '.bis'/'' .                                              Remove file type
@.
@.    #  Make sure files exists
@     call,SUBLCOMMON_ getCabDrwRpt_ (<run>,<cab>i4,<drw>h1,<rpt>i4) .
@     pcr,0,I,-0,n,n,0181 <file>  rnm -2 .
@.
@.    #  With each build bump the version number
@.    #  Details found here: http://semver.org/
@     fdr,SUBLCONFIG_,,,0185 '' 'Parameter' ¬,'VER'  rln 'Siz','Ty' <vsz>i,<vty>h .
@     if <rev> eq ''  ldv <rev>='PATCH' .                                          CAN NOT BE SPACES
@     if <rev> eq 'MAJOR','MINOR','PATCH','NEW' . ; gto 0182 .                  MUST BE ONE OF THESE
@     if <rev> eq NEW  if <ver> gt '',(0183) . ; .
@     if <ver> eq '' . ; gto 0003 .
@     if <rev> eq NEW  ldv <ver>=0.1.0 . ; gto 0184 .                       DEFAULT TO 1.0.0 FOR NEW
@0003:lcv 'b1' <ver> '.' <vpos1>I3 .
@     lcv 'b2' <ver> '.' <vpos2>I3 .
@     chg <mnsz>I2 <vpos2> - <vpos1> .                                       GET MINOR REVISION SIZE
@     inc <vpos1>  dec <mnsz>  ldv <minor>I3=<ver>(<vpos1>-<mnsz>) .       GET MINOR REVESION NUMBER
@     dec <vpos1>  ldv <major>I3=<ver>(1-<vpos1>)  inc <vpos1> .           GET MAJOR REVESION NUMBER
@     inc <vpos2>  ldv <patch>I3=<ver>(<vpos2>-0) .                        GET PATCH REVESION NUMBER
@     if <rev> eq MAJOR  inc <major>  ldv <minor>=0,<patch>=0 .
@     if <rev> eq MINOR  inc <minor>  ldv <patch>=0 .
@     if <rev> eq PATCH  inc <patch> .
@     ldv,p <newver>h16=<major>(P)'.'<minor>(P)'.'<patch>(P) .      SAVE THE NEW VERSION NUMBER
@     ldv,p <ver>h16=<newver> .
@.
@.    #  Start SETUP.INF result
@     brk .
[DISK]
 Level           = <ver>
[NAME]
 Name            = <app>
[TITLE]
 Title           = <appdesc>
[DRAWERS]
@     rnm -1 .
@.
@.    #  Get TOC table headings
@     rsl,ecab$,C,12 .
@     rnm -2 .
@.
@.    #  Loop through drawers to see which ones are available
@     ldv <drawers>h8='BCDEFGHI',<dpos>i1=0 .
@0005:inc <dpos>  if <dpos> eq 9,(0030) .
@     ldv <drw>h1=<drawers>(<dpos>-1) .
@     drw,<cab>,<drw>,0005 <cpl>i3,,,,,<hrpt>i4,,,,,,<desc>s26 .
 Drawer          = <drw>,<cpl>,<desc>
@     brk  lzr,-1,lin+1  add,-0,-1 .
@     rnm -1 .
@.
@.    #  Now loop through each program in that drawer to populate table of contents
@     ldv <rpt>i4=-1 .
@     rsl,ecab$,C,12 .                                                                  TOC headings
@     rnm -2 .
@0010:inc <rpt>  if <rpt> gt <hrpt>,(0020) .
@     lzr,<cab>,<drw>,<rpt>,0010 <lines>i6,,,,,,,,,,<typ>h1 .
@     rdl,<cab>,<drw>,<rpt>,2 2-79 <title>s .
@     if <typ> eq 1 . ; ldv <ext>h3='BIS'  gto 0015 .
@     lcv 'b99' <title> '.' ,<occ>i3 .                                           Find last occurance
@     lcv 'b<occ>' <title> '.' <pos>i3  inc <pos> .                                       Last . pos
@     ldv,p <ext>=<title>(<pos>-0) .                                             Load file extension
@0015:ln+,-2,,1  ldv <nln>i3=stat1$ .
@     ldv,p <title> .
@     wrl,-2,<nln> 'Cab','Dw','Rpt','Lines','Typ','Name','Title' ¬,\
        <cab>,<drw>,<rpt>,<lines>,<ext>,,<title> .
@     gto 0010 .
@.
@.    #  Write TOC file out to current folder
@0020:ldv,p <folder>s26=<desc> .
@     ldv,p <expath>s256=<pth>(p)<folder>'\' .
@     call,SUBLCOMMON_ chkMkDir_ (<pth>,<folder>,<expath>) .
@     ldv,p <tocfile>s256=<expath>(p)'TOC.TXT' .
@     pcw,-2,2 <tocfile> .                                                                Write file
@     gto 0005 .
@.
@.    #  Write SETUP.inf out to folder
@0030:brk .
[RUNS]
 Run             = SUBLWATCHER,3,,,NYNWN,B
 Run             = SUBL,2,,,NYNWN,B
[DATABASE]
 Report          = 56,C,3,56,C,14
 Report          = 56,C,8,56,C,13
@     brk .
@     add,-0,-1 .
@     rnm -1 .
@     pcw,-1,2 <pth>(p)'SETUP.INF' .
@     pcw,ecab$,aedrw$,27,2 <pth>(p)'SETUP.RUN' .
@.
@.    # Build complete message
@     ldv <msg>s80='Build Complete' .
@     call,GROWL_ 0001 ('subwatcher: success!','SUCCESS',<msg>) .
@.
@.    #  Exist
@     gto 0199 .
@.
@.    #  Errors
@0180:ldv <msg>s256='Cannot build on non BIS file type' .
@     call,GROWL_ 0001 ('subwatcher: warning!','FAILED',<msg>) .
@     gto 0199 .
@.
@0181:ldv <msg>s256='Could not locate '<file> .
@     call,GROWL_ 0001 ('sublwatcher: warning!','FAILED',<msg>) .
@     gto 0199 .
@.
@0182:ldv <msg>s80='Not a valid revion, options are MAJOR, MINOR, PATCH, or NEW' .
@     call,GROWL_ 0001 ('sublwatcher: warning!','FAILED',<msg>) .
@     gto 0199 .
@.
@0183:ldv <msg>s80='Version already exists cannot be NEW' .
@     call,GROWL_ 0001 ('sublwatcher: warning!','FAILED',<msg>) .
@     gto 0199 .
@.
@0184:ldv <msg>s80='Must enter with a version level of NEW' .
@     call,GROWL_ 0001 ('sublwatcher: warning!','FAILED',<msg>) .
@     gto 0199 .
@.
@0185:ldv <msg>s80='Version not present in SUBLCFG table' .
@     call,GROWL_ 0001 ('sublwatcher: warning!','FAILED',<msg>) .
@     gto 0199 .
@.

@.    #  Exit
@0199:return .
